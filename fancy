#!/usr/bin/env perl
use warnings;
use strict;
use Getopt::Long qw( GetOptionsFromString );
my $Delay     = 0;
my $Timestamp = 0;
my $Count     = 1;
my $Width     = `tput cols`;
my $Height    = `tput lines`;
my $Winch     = undef;
my $Date_Fmt1 = qr{\w+ +\d+ \d\d:\d\d:\d\d};
my $Date_Fmt2 = qr{\d{4}-\d\d-\d\d \d\d:\d\d:\d\d \S+};
my $Date_Re   = qr{$Date_Fmt1|$Date_Fmt2};
local $SIG{WINCH} = sub { $Winch = 1 };

if ( @ARGV and $ARGV[0] =~ m{^-} ) {
    my $arg = shift @ARGV;
    $arg =~ s{^-(\w)([^=]+)}{-$1=$2};
    $arg .= q{=} . shift @ARGV if ( @ARGV and $arg !~ m{=} );
    my $r = GetOptionsFromString( $arg, 'n=i' => \$Delay );
    die "Usage: fancy [ -n <seconds> ] [ <command> ]\n" if ( not $r );
}

print "\e[H\e[2J";

if (@ARGV) {
    while (1) {
        my $fh = undef;
        open $fh, q{-|}, @ARGV or die;
        $Delay ||= 2;
        $Timestamp = 0;
        $Count     = 1;
        draw($fh);
        close $fh or die $!;
        sleep $Delay;
    }
}
else {
    my $fh = *STDIN;
    $Delay //= 0;
    draw($fh);
}

sub draw {
    my $fh = shift;
    while ( my $line = <$fh> ) {
        clear($line) or next if ($Delay);
        $line =~ s/#033\[/\e[/g;
        $line =~ s/\\x2d/-/g;
        chomp $line;
        if ( $line =~ m{^($Date_Re) (\S+) (.*?:) (.*)$} ) {
            my ( $date, $host, $process, $msg ) = ( $1, $2, $3, $4 );
            my $color = get_color($msg);
            printf "\e[95m%s \e[96m%s \e[94m%s $color%s\e[0m\n",
                $date, $host, $process, $msg;
        }
        elsif ( $line =~ m{^($Date_Re) (\S+) (.*)$} ) {
            my ( $date, $host, $msg ) = ( $1, $2, $3 );
            my $color = get_color($msg);
            printf "\e[95m%s \e[96m%s $color%s\e[0m\n",
                $date, $host, $msg;
        }
        else {
            my $color = get_color($line);
            print $color, $line, "\e[0m\n";
        }
    }
    return;
}

sub clear {
    my $line  = shift;
    my $clear = undef;
    my $rv    = 1;
    my $now   = time;
    if ($Winch) {
        $Winch  = undef;
        $Width  = `tput cols`;
        $Height = `tput lines`;
    }
    my $span = int( length($line) / $Width + 1 );
    $Count += $span;
    if (@ARGV) {
        if ( not $Timestamp ) {
            $clear = 1;
        }
        else {
            $rv = undef if ( $Count >= $Height );
        }
    }
    elsif ( $now - $Timestamp > $Delay or $Count >= $Height ) {
        $clear = $now - $Timestamp;
    }
    if ($clear) {
        $Count     = $span;
        $Timestamp = $now;
        print "\e[H";
        print "\e[2J" if ( not @ARGV );
    }
    return $rv;
}

sub get_color {
    my $line  = shift;
    my $color = undef;
    if ( $line =~ m{ERROR:} ) {
        $color = "\e[30;101m";
    }
    elsif ( $line =~ m{WARNING:} ) {
        $color = "\e[30;103m";
    }
    elsif ( $line =~ m{INFO:|DEBUG:|systemd[[]} ) {
        $color = "\e[90m";
    }
    elsif ( $line =~ m{auth|ssh|sftp}i ) {
        $color = "\e[92m";
    }
    elsif ( $line =~ m{fail|error}i ) {
        $color = "\e[91m";
    }
    elsif ( $line =~ m{warning}i ) {
        $color = "\e[93m";
    }
    else {
        $color = "\e[0m";
    }
    return $color;
}
